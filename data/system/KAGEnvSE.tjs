/**
 * 環境SEオブジェクト
 */
class KAGEnvSE {

    var env;
    var id;
    var name;  // 参照しているファイル名
	var loop;  // ループしてるか
	var volume;
	var count; // 参照されたカウント値
	
	var play;
	var stop;
	var fade;
	var wait;
    var waitFade;

    /**
     * セーブ処理
     */
    function onStore(f) {
		f.name  = name;
		f.loop  = loop;
		f.volume = volume;
		f.id    = id;
		f.count = count;
	}

	function onRestore(f) {
		if (f !== void) {
			name = f.name;
			loop = f.loop;
			id   = f.id;
			count = f.count;
			if (name != "" && loop) {
				setPlay(name, %[loop:loop]);
				if (f.volume !== void) {
					fade.volume = f.volume;
				}
			}
		}
	}
	
    /**
     * コンストラクタ
     */
    function KAGEnvSE(env, id) {
        this.env = env;
        this.id = id;
    }

    /**
     * 再生処理
     * @param param 再生対象ファイル
     */
	function setPlay(param, elm) {
		if (param !== void) {
			stop = void;
			if (kag.skipMode<SKIP_STOP || elm.loop) {
				name = param;
				loop = elm.loop;
				play = %[];
				(Dictionary.assign incontextof play)(elm, false);
				if (play.fade === void && play.time === void) {
					fade = %[];
					fade.volume = volume = 100;
				}
			} else {
				play = void;
				name = void;
				loop = void;
			}
			doflag = true;
		}
    }

    /**
     * 停止処理
     * @param param フェードアウト時間
     */
	function setStop(param, elm) {
		play = void;
		fade = void;
		name = void;
		loop = void;
		var time;
		if (elm !== void && elm.time !== void) {
			time = +elm.time;
		} else {
			time = +param;
		}
		stop = time;
		doflag = true;
    }

    /**
     * 音量フェード
     * @param param フェード時間
     */
	function setFade(param, elm) {
		fade = %[];
		fade.volume = volume = +param;
		fade.time   = elm.time;
    }

    /**
     * 終了待ち
     */
    function setWait(param, elm) {
        wait = %[];
        (Dictionary.assign incontextof wait)(elm, false);
        wait.buf = id;
        if (wait.canskip === void) {
            wait.canskip = true;
        }
    }

    /**
     * フェード終了待ち
     */
    function setWaitFade(param, elm) {
		waitFade = %[];
		(Dictionary.assign incontextof waitFade)(elm, false);
		waitFade.buf = id;
		if (waitFade.canskip === void) {
			waitFade.canskip = true;
		}
    }

    var secommands = %[
    tagname : null, 
    storage : setPlay incontextof this,
    play : setPlay incontextof this,
    stop : setStop incontextof this,
    fade : setFade incontextof this,
	wait : setWait incontextof this,
    waitfade : setWaitFade incontextof this,
    loop : null,
    time : null,
    start : null,
    canskip : null,
    buf : null,
    name : null,
    nosync : null,
        ];

    var doflag;

    /**
     * コマンドの実行
     * @param cmd コマンド
     * @param param パラメータ
     * @param elm 他のコマンドも含む全パラメータ
     * @return 実行が行われた場合 true
     */
    function doCommand(cmd, param, elm) {
        var func;
        if ((func = secommands[cmd]) !== void) {
            if (func != null) {
                func(param, elm);
            }
            return true;
        }
        // 再生コマンドとみなす
		setPlay(cmd, elm);
        return true;
    }

    /**
     * KAG タグ処理
     * @param elm 引数
     */
    function tagfunc(elm) {
		// dm("SE 用ファンクション呼び出し!");
		wait = void;
        waitFade = void;
		doflag = false;
		foreach(elm, doCommand);
		if (!doflag && elm.tagname != "se" && elm.tagname != "allse") {
			setPlay(elm.tagname, elm);
        }
		sync();
		if (waitFade !== void) {
			return kag.waitSEFade(waitFade);
		} else if (wait !== void) {
			return kag.waitSEStop(wait);
		}
		return 0;
    }

	function sync() {
		if (!kag.skipNoDisp) {
			if (fade !== void) {
				if (fade.time === void) {
					kag.se[id].volume = fade.volume;
				} else {
					kag.se[id].fade(fade);
				}
				fade = void;
			}
			if (play !== void) {
				if (kag.skipMode<SKIP_STOP || play.loop || !kag.nosewhenskip) {
					var time = +play.time;
					if (time > 0)  {
						kag.se[id].fadeIn(%[ storage:name, loop:play.loop, time:time, start:play.start]);
					} else {
						kag.se[id].play(%[ storage:name, loop:play.loop, start:play.start]);
					}
				}
				play = void;
			}
		}
		if (stop !== void) {
			if (kag.skipMode == SKIP_NONE && stop > 0) {
				kag.se[id].fadeOut(%[time:stop]);
			} else {
				kag.se[id].stop();
			}
			stop = void;
		}
	}

};
